pkgname=scalafmt-native
pkgver=2.4.1
pkgrel=1
pkgdesc="code formatter for the Scala programming language. (native pre-compiled binary)"
arch=("x86_64")
url="https://scalameta.org/scalafmt/"
license=('Apache')
provides=('scalafmt')
conflicts=('scalafmt')
depends=('sbt' 'native-image-jdk11-bin')
source=("https://github.com/scalameta/scalafmt/archive/v${pkgver}.tar.gz")
sha256sums=('da86c900b547ae453819b5038903c6f1afdfb9c18ed32e04cc7bd1bca0dc0f39')

build() {
  cd "${srcdir}/scalafmt-${pkgver}"
  
  env CI=maybe \
      PATH=/usr/lib/jvm/java-11-graalvm/bin:$PATH \
    sbt cli/graalvm-native-image:packageBin
}

package() {
  cd "${srcdir}/scalafmt-${pkgver}"
  
  install -Dm755 "scalafmt-cli/target/graalvm-native-image/cli" "${pkgdir}/usr/bin/scalafmt"
} 
