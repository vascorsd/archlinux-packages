pkgname=scala-cli-native-bin
pkgver=0.1.20
pkgrel=1
pkgdesc='Scala CLI is a command-line tool to interact with the Scala language.'
arch=('x86_64')
url='https://scala-cli.virtuslab.org'
license=('Apache')
conflicts=('scala-cli')
depends=('java-runtime-headless>=8')
source=("scala-cli-$pkgver.gz::https://github.com/VirtusLab/scala-cli/releases/download/v$pkgver/scala-cli-$arch-pc-linux.gz")
sha256sums=('7a123a8117c76d0c118d823cf0e6d617d2a990ff209349574dd7d844619fa328')
options=(!strip)
noextract=("scala-cli-$pkgver.gz")

prepare() {
  # it seems the completions are generated based on the binay
  # name being called on, so using the binary with the version
  # attached breaks it completly.
  #
  # options:
  #  - need to change to proper name first and only then call 
  #    the thing and putting it in place;
  #  - avoid auto unzipping and handle it manually
  gunzip --stdout "scala-cli-$pkgver.gz" > scala-cli

  # generate the completions files
  chmod +x scala-cli
  
  ./scala-cli install-completions --shell bash --env > \
    bash-complete

  # drop the zsh ones.
  # the output seems nonsensincal and I will not test it anyway
}

package() {
  install -Dm755 scala-cli -t "$pkgdir/usr/bin/"

  install -Dm644 bash-complete \
    "$pkgdir/usr/share/bash-completion/completions/scala-cli"
}
