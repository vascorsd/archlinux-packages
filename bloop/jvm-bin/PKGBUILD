pkgname=bloop-jvm-bin
pkgver=1.3.4
pkgrel=2
pkgdesc="Bloop gives you fast edit/compile/test workflows for Scala. (jvm launcher version)"
arch=(any)
url="https://scalacenter.github.io/bloop/"
provides=('bloop')
conflicts=('bloop')
license=('Apache')
depends=('java-environment>=8' 'python')
makedepends=('coursier')


_nailgunver=d7ed5db

source=(
    "bloop-${pkgver}.tar.gz::https://github.com/scalacenter/bloop/archive/v$pkgver.tar.gz"
    "nailgun-client::https://raw.githubusercontent.com/scalacenter/nailgun/${_nailgunver}/pynailgun/ng.py"
)
sha256sums=(
    '0bb6815d22d2c94c15b2637d7cbf73b8b7df62c83a1c747ff29c55a3f6087dcd'
    '0ae9887ba306b69be12c53885914b2efb4ec2f635ce25aa2f8f06e0b6a113253'
)

build() {
  cd "$srcdir"
  
  # basically there are a bunch of resources to install
  # and the bloop server and client.
  # the client is the thing called nailgun, which is python, wtv that is.
  # the server we generate with a magic incantation of the coursier that embeds 
  # a magically bootstrapable binary or something like that. if not is close.
  
  coursier bootstrap ch.epfl.scala:bloop-frontend_2.12:$pkgver \
    -r bintray:scalameta/maven \
    -r bintray:scalacenter/releases \
    -r https://oss.sonatype.org/content/repositories/staging \
    -o blp-server -f --main bloop.Server
  
  # fix paths
  cd "$srcdir/bloop-$pkgver"
  sed -i "s|__BLOOP_INSTALLATION_TARGET__|/usr/bin|g" etc/systemd/bloop.service

  sed -i "s|__BLOOP_INSTALLATION_TARGET__/xdg|/usr/share/pixmaps|g" etc/xdg/bloop.desktop
  sed -i "s|__BLOOP_INSTALLATION_TARGET__|/usr/bin|g" etc/xdg/bloop.desktop
}

package() {
  cd "$srcdir"

  ## binaries
  install -Dm755 blp-server "$pkgdir"/usr/bin/blp-server
  install -Dm755 nailgun-client "$pkgdir"/usr/bin/bloop

  cd "$srcdir/bloop-$pkgver"
  
  # desktop file
  install -Dm644 etc/xdg/bloop.png "$pkgdir"/usr/share/pixmaps/bloop.png
  install -Dm644 etc/xdg/bloop.desktop "$pkgdir"/usr/share/applications/bloop.desktop

  # shell completion
  install -Dm644 etc/bash/bloop "$pkgdir"/etc/bash_completion.d/bloop
  install -Dm644 etc/zsh/_bloop "$pkgdir"/usr/share/zsh/site-functions/_bloop
  install -Dm644 etc/fish/bloop.fish "$pkgdir"/usr/share/fish/vendor_completions.d/bloop.fish

  # systemd service
  install -Dm644 etc/systemd/bloop.service "$pkgdir"/usr/lib/systemd/user/bloop.service
}
