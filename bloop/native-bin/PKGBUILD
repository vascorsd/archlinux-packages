_version=1.4.3

pkgname=bloop-native-bin
pkgver="${_version//-/_}"
pkgrel=1
pkgdesc="Bloop gives you fast edit/compile/test workflows for Scala. (native launcher version)"
arch=(any)
url="https://scalacenter.github.io/bloop/"
provides=('bloop')
conflicts=('bloop')
license=('Apache')
depends=('java-environment>=8')

source=(
    "bloop::https://github.com/scalacenter/bloop/releases/download/v${_version}/bloop-x86_64-pc-linux"

    # shells completions
    "https://github.com/scalacenter/bloop/releases/download/v${_version}/bash-completions"
    "https://github.com/scalacenter/bloop/releases/download/v${_version}/fish-completions"
    "https://github.com/scalacenter/bloop/releases/download/v${_version}/zsh-completions"

    # static resources, not in release assets but in git repo
    "https://raw.githubusercontent.com/scalacenter/bloop/v${_version}/etc/xdg/bloop.desktop"
    "https://raw.githubusercontent.com/scalacenter/bloop/v${_version}/etc/xdg/bloop.png"
    
    #"https://raw.githubusercontent.com/scalacenter/bloop/v${_version}/etc/systemd/bloop.service"
    "bloop.service"
    # NOTE: not possible to used socket activation without the app itself being prepared for it.
    #"bloop.socket"
)
sha256sums=(
    '2ec43503096e71d54b2ff20da42b22aaf3e11212a3926ddcecbe26534e86c331'

    'da6b7ecd4109bd0ff98b1c452d9dd9d26eee0d28ff604f6c83fb8d3236a6bdd1'
    'a012a5cc76b57dbce17fad237f3b97bea6946ffc6ea0b61ac2281141038248dd'
    '58d32c3f005f7791237916d1b5cd3a942115236155a0b7eba8bf36391d06eff7'

    '4aab103b8e8b5d6e95c9f5721dbc6038f64457f39b079cb0d91efa0476c889b4'
    '53d945f692f5f5e9e64793f8b1926c3731bac365041ce514dec13d89376c1e6a'
    
    'cbd783c5037ce08b197ff8334c2ad4366be0624dbe9cf2da41ef1832f5628b66'
)

build() {
  cd "$srcdir"

  # fix paths
  #sed -i "s|__BLOOP_INSTALLATION_TARGET__|/usr/bin|g" bloop.service

  sed -i "s|__BLOOP_INSTALLATION_TARGET__/xdg|/usr/share/pixmaps|g" bloop.desktop
  sed -i "s|__BLOOP_INSTALLATION_TARGET__|/usr/bin|g" bloop.desktop
}

package() {
  cd "$srcdir"

  ## binary
  install -Dm755 bloop "$pkgdir"/usr/bin/bloop

  # shell completion
  install -Dm644 bash-completions "$pkgdir"/etc/bash_completion.d/bloop
  install -Dm644 zsh-completions "$pkgdir"/usr/share/zsh/site-functions/_bloop
  install -Dm644 fish-completions "$pkgdir"/usr/share/fish/vendor_completions.d/bloop.fish

  # desktop file
  install -Dm644 bloop.png "$pkgdir"/usr/share/pixmaps/bloop.png
  install -Dm644 bloop.desktop "$pkgdir"/usr/share/applications/bloop.desktop

  # systemd service
  install -Dm644 bloop.service "$pkgdir"/usr/lib/systemd/user/bloop.service
  #install -Dm644 bloop.socket "$pkgdir"/usr/lib/systemd/user/bloop.socket
}
