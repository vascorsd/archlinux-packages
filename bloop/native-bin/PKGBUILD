pkgname=bloop-native-bin
pkgver=1.5.4
pkgrel=1
pkgdesc="Bloop gives you fast edit/compile/test workflows for Scala. (native pre-compiled binary)"
arch=("x86_64")
url="https://scalacenter.github.io/bloop/"
provides=('bloop')
conflicts=('bloop')
license=('Apache')
options=(!strip)

source=(
    # native architecture binary
    "bloop::https://github.com/scalacenter/bloop/releases/download/v${pkgver}/bloop-${arch}-pc-linux"

    # shells completions
    "https://github.com/scalacenter/bloop/releases/download/v${pkgver}/bash-completions"
    "https://github.com/scalacenter/bloop/releases/download/v${pkgver}/fish-completions"
    "https://github.com/scalacenter/bloop/releases/download/v${pkgver}/zsh-completions"

    # static resources, not in release assets but in git repo
    #"https://raw.githubusercontent.com/scalacenter/bloop/v${pkgver}/etc/systemd/bloop.service"
    # moved to be inline here
    "bloop.service"
)
sha256sums=('abc2b9773ccb850f13e95ce1a09e621a4d32cfda4e3eb3ff5787a24f0cb3960a'

            'da6b7ecd4109bd0ff98b1c452d9dd9d26eee0d28ff604f6c83fb8d3236a6bdd1'
            '78511247a88f1d10d5886bfe164dec0af3e0540864d8bd60086e0b9df5cefefe'
            '58d32c3f005f7791237916d1b5cd3a942115236155a0b7eba8bf36391d06eff7'

            'SKIP')


prepare() {
  # fix paths
  sed -i "s|__BLOOP_INSTALLATION_TARGET__|/usr/bin|g" bloop.service
}

package() {
  cd "$srcdir"

  ## binary
  install -Dm755 bloop "$pkgdir"/usr/bin/bloop

  # shell completion
  install -Dm644 bash-completions "$pkgdir"/usr/share/bash-completion/completions/bloop
  install -Dm644 zsh-completions "$pkgdir"/usr/share/zsh/site-functions/_bloop
  install -Dm644 fish-completions "$pkgdir"/usr/share/fish/vendor_completions.d/bloop.fish

  # systemd service
  install -Dm644 bloop.service "$pkgdir"/usr/lib/systemd/user/bloop.service
}
