# Adapted from https://aur.archlinux.org/packages/coursier/

_version=2.0.0-RC6-18

pkgname=coursier-jvm-bin
pkgver="${_version//-/_}"
pkgrel=1
pkgdesc="Pure Scala Artifact Fetching. (jvm launcher version)"
arch=('any')
url="http://get-coursier.io"
license=('Apache')
depends=('java-environment>=8' 'bash')
source=("builder::https://github.com/coursier/coursier/releases/download/v${_version}/coursier")
sha256sums=('c26e66034792464bb82a8f0f5060e69e9fc0f9380eee2f7ca1e801eb5d4ad56f')
noextract=("builder")

build() {
  cd "${srcdir}"
  mkdir -p cache bin
  export COURSIER_CACHE="${srcdir}/cache"
  
  sh ./builder bootstrap \
    "io.get-coursier::coursier-cli:${_version}" \
    --java-opt "-noverify" \
    --no-default \
    -r central \
    -r typesafe:ivy-releases \
    -f -o "bin/coursier-jvm" \
    --standalone
}

package() {
  install -D -m755 "${srcdir}/bin/coursier-jvm" "${pkgdir}/usr/bin/coursier-jvm"
}
